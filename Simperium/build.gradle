
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies { classpath 'com.android.tools.build:gradle:0.12.+' }
}

apply plugin: 'android-library'
apply plugin: 'maven'

version gitVersion()

group "com.simperium"

repositories {
    mavenCentral()
    maven { url "http://simperium.github.io/simperium-android" }
}

android {
  
    publishNonDefault true

    dependencies {
        compile 'com.koushikdutta.async:androidasync:1.2.6'
        compile 'com.android:volley:4.4.2.1'
        compile 'org.thoughtcrime.ssl.pinning:AndroidPinning:1.0.0'
    }

    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        applicationId "com.simperium"
        versionCode 1
        versionName project.version

        // Calculating PIN for certificate: OU=Domain Control Validated, CN=*.simperium.com
        // Pin Value: e25695097d04927c9d90206333a687a7b1f99708
        buildConfigField 'String', 'SIMPERIUM_COM_SPKI', '"e25695097d04927c9d90206333a687a7b1f99708"'
    }

    productFlavors {
        // client is the main production build
        client
        // support build is for running tests and includes mock code
        support
    }

}

task versionConfig(group: "build", description: "Generate version class") {
    def dir = "${buildDir}/generated/source/version"
    def file = new File(dir, "com/simperium/Version.java")

    android.sourceSets.main.java.srcDir dir

    doLast {
        def describe = gitDescribe()

        logger.info "Version: ${describe}"

        file.getParentFile().mkdirs()
        def writer = new FileWriter(file)
        writer.write("""/* auto-generated file do not modify */
package com.simperium;

public final class Version {

    public static final String LIBRARY_NAME = "android";

    public static final String NAME = "android-${project.version}";

    /* project.version */
    public static final String NUMBER = "${project.version}";

    /* git rev-parse --short --verify HEAD */
    public static final String BUILD = "${gitHash()}";

    /* git describe --always --dirty=-dirty */
    public static final String DESCRIBE = "${gitDescribe()}";
}
""")
        writer.close()
    }
}

if (project.hasProperty("repository")) {
  afterEvaluate {
    uploadArchives {
      repositories {
        mavenDeployer {
          repository(url: project.repository)
          pom.version = project.version
          pom.artifactId = 'simperium-android'
        }
      }
    }
  }
}

tasks.preBuild.dependsOn versionConfig
